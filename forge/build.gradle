plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    include(forgeRuntimeLibrary("com.theokanning.openai-gpt3-java:service:0.11.0")) { transitive = false }
    include(forgeRuntimeLibrary("com.theokanning.openai-gpt3-java:api:0.11.0")) { transitive = false }
    include(forgeRuntimeLibrary("com.theokanning.openai-gpt3-java:client:0.11.0")) { transitive = false }

    include(forgeRuntimeLibrary("io.reactivex.rxjava2:rxjava:2.0.0")) { transitive = false }

    include(api(forgeRuntimeLibrary('com.squareup.retrofit2:retrofit:2.9.0'))) { transitive = false }
    include(forgeRuntimeLibrary('com.squareup.retrofit2:adapter-rxjava2:2.9.0')) { transitive = false }
    include(forgeRuntimeLibrary('com.squareup.retrofit2:converter-jackson:2.9.0')) { transitive = false }

    include(forgeRuntimeLibrary("com.squareup.okhttp3:okhttp:3.14.9")) { transitive = false }
    include(forgeRuntimeLibrary("com.squareup.okio:okio:1.17.2")) { transitive = false }

    include(forgeRuntimeLibrary("com.fasterxml.jackson.core:jackson-annotations:2.10.1")) { transitive = false }
    include(forgeRuntimeLibrary("com.fasterxml.jackson.core:jackson-core:2.10.1")) { transitive = false }
    include(forgeRuntimeLibrary("com.fasterxml.jackson.core:jackson-databind:2.10.1")) { transitive = false }

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
